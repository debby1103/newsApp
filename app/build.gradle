apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "jaydenxiao.com.androidfire"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 4
        versionName "1.0.3"

    }
    buildTypes {
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //apk命名
            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 这里修改apk名称 
                        def fileName = outputFile.name.replace("app", "newsAPP_${defaultConfig.versionName}.${defaultConfig.versionCode}_${releaseTime()}")
                        outputFileName = fileName
                    }
                }
            }

        }
        debug{
            buildConfigField "boolean", "LOG_DEBUG", "true"
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
        }
    }
    //设置虚拟机堆内存空间大小，避免在编译期间OOM
    dexOptions {
       // incremental true
        javaMaxHeapSize "4g"
    }
    //打包不检查
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }
}


buildscript {
    repositories {
        maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
        maven { url 'http://maven.aliyun.com/nexus/content/repositories/jcenter' }
        mavenCentral()
        google()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
    }
}
dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api  project(':library:photoPicker')//图片选择
    api  project(':library:irecyclerview')//万能适配器和recyclerview
    api  project(':library:oneKeyShareSDK')//分享
    api  project(':library:jcvideoplayer')//视频播放
    api  project(':library:selectordialog')//常用提示框
    implementation 'com.jakewharton:butterknife:10.1.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:10.1.0'
}
